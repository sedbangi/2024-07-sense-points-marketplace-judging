Quaint Tangelo Raccoon

Medium

# Fee collector should pay `rewardTokenFee` when redeem rewards for `pTokenFee`.

## Summary
Vault has two kinds of fees: `pTokenFee` and `rewardTokenFee`.
Due to some errors, `feeCollector` should pay `rewardTokenFee` while redeem rewards for `pTokenFee`, but users can avoid `rewardTokenFee` for some `PTokens`.

## Vulnerability Detail
`PointTokenVault.claimPTokens()` function is following.
```solidity
    function claimPTokens(Claim calldata _claim, address _account, address _receiver) public {
        bytes32 pointsId = _claim.pointsId;

        bytes32 claimHash = keccak256(abi.encodePacked(_account, pointsId, _claim.totalClaimable));
146:    _verifyClaimAndUpdateClaimed(_claim, claimHash, _account, claimedPTokens);

        if (address(pTokens[pointsId]) == address(0)) {
            revert PTokenNotDeployed();
        }

        if (_account != _receiver && !trustedClaimers[_account][_receiver]) {
            revert NotTrustedClaimer();
        }

        uint256 pTokenFee = FixedPointMathLib.mulWadUp(_claim.amountToClaim, mintFee);
        pTokenFeeAcc[pointsId] += pTokenFee;

159:    pTokens[pointsId].mint(_receiver, _claim.amountToClaim - pTokenFee); // Subtract mint fee.

        emit PTokensClaimed(_account, _receiver, pointsId, _claim.amountToClaim, pTokenFee);
    }
```
Assume that `_account == _receiver`.
`_verifyClaimAndUpdateClaimed()` of `L146` is following.
```solidity
    function _verifyClaimAndUpdateClaimed(
        Claim calldata _claim,
        bytes32 _claimHash,
        address _account,
        mapping(address => mapping(bytes32 => uint256)) storage _claimed
    ) internal {
        --- SKIP ---
        // Update the total claimed amount.
        unchecked {
294:        _claimed[_account][pointsId] = alreadyClaimed + amountToClaim;
        }
    }
```
As can be seen, the claimer (`_account`) get `PTokens` of `amountToClaim - pTokenFee` on `L159` but `claimedPTokens[_account][pointsId]` is increased by `amountToClaim`.
On the other hand, `collectFees()` function is following.
```solidity
    function collectFees(bytes32 _pointsId) external {
        (uint256 pTokenFee, uint256 rewardTokenFee) = (pTokenFeeAcc[_pointsId], rewardTokenFeeAcc[_pointsId]);

        if (pTokenFee > 0) {
347:        pTokens[_pointsId].mint(feeCollector, pTokenFee);
            pTokenFeeAcc[_pointsId] = 0;
        }

        if (rewardTokenFee > 0) {
            // There will only be a positive rewardTokenFee if there are reward tokens in this contract available for transfer.
            redemptions[_pointsId].rewardToken.safeTransfer(feeCollector, rewardTokenFee);
            rewardTokenFeeAcc[_pointsId] = 0;
        }

        emit FeesCollected(_pointsId, feeCollector, pTokenFee, rewardTokenFee);
    }
```
The `feeCollector` get `pTokenFee` `PTokens` on `L347` but `claimedPTokens[feeCollector][pointsId]` is not increased.
Users can avoid `rewardTokenFee` up to `claimedPTokens` variable in the following `redeemRewards()` function.
```solidity
    function redeemRewards(Claim calldata _claim, address _receiver) public {
        --- SKIP ---
        uint256 claimed = claimedPTokens[msg.sender][pointsId];
        uint256 feelesslyRedeemed = feelesslyRedeemedPTokens[msg.sender][pointsId];

        // The amount of pTokens that are free to redeem without fee.
        uint256 feelesslyRedeemable = claimed - feelesslyRedeemed;
        --- SKIP ---
    }
```
Therefore, the following scenario is available:
1. Assume that `user1` claimed `1000` `PTokens` and `mintFee` is `10%`.
2. `user1` pays `100` `PTokens` to `feeCollector` and receives only `900` `PTokens` but `claimedPTokens[_account][pointsId]` variable increases by `1000`.
3. `user1` redeem his own `900` `PTokens` feelessly. Not only that, `user1` can buy `100` more `PTokens` from others and redeem them too feelessly.
4. On the other hand, `feeCollector` has to pay `rewardTokenFee` when he redeem his own `100` `PTokens`.

## Impact
`feeCollector` has to pay `rewardTokenFee` when he redeem his own `pTokenFee` and it makes the fee calculation incorrect.
Users can avoid `rewardTokenFee` for the `pTokenFee` `PTokens`.

## Code Snippet
https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/point-tokenization-vault/contracts/PointTokenVault.sol#L142-L162
https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/point-tokenization-vault/contracts/PointTokenVault.sol#L343-L358

## Tool used

Manual Review

## Recommendation
Modify `PointTokenVault.claimPTokens()` function as follows.
```solidity
    function claimPTokens(Claim calldata _claim, address _account, address _receiver) public {
        bytes32 pointsId = _claim.pointsId;

        bytes32 claimHash = keccak256(abi.encodePacked(_account, pointsId, _claim.totalClaimable));
        _verifyClaimAndUpdateClaimed(_claim, claimHash, _account, claimedPTokens);

        if (address(pTokens[pointsId]) == address(0)) {
            revert PTokenNotDeployed();
        }

        if (_account != _receiver && !trustedClaimers[_account][_receiver]) {
            revert NotTrustedClaimer();
        }

        uint256 pTokenFee = FixedPointMathLib.mulWadUp(_claim.amountToClaim, mintFee);
        pTokenFeeAcc[pointsId] += pTokenFee;
++      claimedPTokens[_account][pointsId] -= pTokenFee;

        pTokens[pointsId].mint(_receiver, _claim.amountToClaim - pTokenFee); // Subtract mint fee.

        emit PTokensClaimed(_account, _receiver, pointsId, _claim.amountToClaim, pTokenFee);
    }
```
And modify `PointTokenVault.collectFees()` function as follows.
```solidity
    function collectFees(bytes32 _pointsId) external {
        (uint256 pTokenFee, uint256 rewardTokenFee) = (pTokenFeeAcc[_pointsId], rewardTokenFeeAcc[_pointsId]);

        if (pTokenFee > 0) {
            pTokens[_pointsId].mint(feeCollector, pTokenFee);
++          claimedPTokens[feeCollector][_pointsId] += pTokenFee;
            pTokenFeeAcc[_pointsId] = 0;
        }

        if (rewardTokenFee > 0) {
            // There will only be a positive rewardTokenFee if there are reward tokens in this contract available for transfer.
            redemptions[_pointsId].rewardToken.safeTransfer(feeCollector, rewardTokenFee);
            rewardTokenFeeAcc[_pointsId] = 0;
        }

        emit FeesCollected(_pointsId, feeCollector, pTokenFee, rewardTokenFee);
    }
```