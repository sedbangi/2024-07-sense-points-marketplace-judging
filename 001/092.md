Original Beige Leopard

Medium

# `claimPTokens` can be called by anyone, allowing attackers to claim pTokens to trusted addresses to force the collection of redemption fees.


## Summary

`claimPTokens` can be called by anyone, allowing attackers to claim pTokens to trusted addresses to force the collection of redemption fees.

## Vulnerability Detail

First, we should understand the background:

1. Users can set a trusted claimer. Anyone can call `claimPTokens()` to claim a user's pToken to the one of user's trusted claimer.
2. When users are redeeming rewards, redemptionFee may be collected if the user redeems more pToken than he claimed.

So the issue arises: an attacker can perform a grief attack to claim pTokens to a trusted address, and when that address redeems rewards, the redemptionFee is forced to be collected. Example:

1. User A has 100 pTokens yet to claim. User A sets B to his trusted claimer.
2. Attacker comes and calls `claimPTokens()` and claims 100 pTokens from user A to B (Note: The claimed amount of pTokens only counts the address that is claimed from, and not the address that it is claimed to. In this case, for A is 100, for B is 0).
3. User B has 100 pTokens. When B wants to redeem them, redemptionFee must be collected, since the claimed amount of tokens for B is 0. In contrast, if A claimed the 100 pTokens for himself and redeems them, redemptionFee would not be collected.

Whether the tokens should be claimed by A or B should only rely solely on A or B's decision, and not by any random person.

Yes, B could transfer the pTokens back to A and have A redeem the rewards, but that would be an extra transaction, which would cause extra unneeded gas fees.

```solidity
    // ISSUE: This function can be called by anyone.
>   function claimPTokens(Claim calldata _claim, address _account, address _receiver) public {
        bytes32 pointsId = _claim.pointsId;

        bytes32 claimHash = keccak256(abi.encodePacked(_account, pointsId, _claim.totalClaimable));
        _verifyClaimAndUpdateClaimed(_claim, claimHash, _account, claimedPTokens);

        if (address(pTokens[pointsId]) == address(0)) {
            revert PTokenNotDeployed();
        }

        if (_account != _receiver && !trustedClaimers[_account][_receiver]) {
            revert NotTrustedClaimer();
        }

        uint256 pTokenFee = FixedPointMathLib.mulWadUp(_claim.amountToClaim, mintFee);
        pTokenFeeAcc[pointsId] += pTokenFee;

        pTokens[pointsId].mint(_receiver, _claim.amountToClaim - pTokenFee); // Subtract mint fee.

        emit PTokensClaimed(_account, _receiver, pointsId, _claim.amountToClaim, pTokenFee);
    }

    function trustClaimer(address _account, bool _isTrusted) public {
        trustedClaimers[msg.sender][_account] = _isTrusted;
        emit TrustClaimer(msg.sender, _account, _isTrusted);
    }

    function redeemRewards(Claim calldata _claim, address _receiver) public {
        ...

        uint256 pTokensToBurn = FixedPointMathLib.divWadUp(amountToClaim, rewardsPerPToken);
        pTokens[pointsId].burn(msg.sender, pTokensToBurn);

        uint256 claimed = claimedPTokens[msg.sender][pointsId];
        uint256 feelesslyRedeemed = feelesslyRedeemedPTokens[msg.sender][pointsId];

        // The amount of pTokens that are free to redeem without fee.
        uint256 feelesslyRedeemable = claimed - feelesslyRedeemed;

        uint256 rewardsToTransfer;
        uint256 fee;

        if (feelesslyRedeemable >= pTokensToBurn) {
            // If all of the pTokens are free to redeem without fee.
            rewardsToTransfer = amountToClaim;
            feelesslyRedeemedPTokens[msg.sender][pointsId] += pTokensToBurn;
        } else {
            // If some or all of the pTokens need to be charged a fee.
            uint256 redeemableWithFee = pTokensToBurn - feelesslyRedeemable;
            // fee = amount of pTokens that are not feeless * rewardsPerPToken * redemptionFee
>           fee = FixedPointMathLib.mulWadUp(
                FixedPointMathLib.mulWadUp(redeemableWithFee, rewardsPerPToken), redemptionFee
            );

            rewardTokenFeeAcc[pointsId] += fee;
            rewardsToTransfer = amountToClaim - fee;

            if (feelesslyRedeemed != claimed) {
                feelesslyRedeemedPTokens[msg.sender][pointsId] = claimed;
            }
        }

        params.rewardToken.safeTransfer(_receiver, rewardsToTransfer);

        emit RewardsClaimed(msg.sender, _receiver, pointsId, rewardsToTransfer, fee);
    }
```

## Impact

Users would be forced to pay the redemptionFee, or they need to perform extra transactions (which would cause extra gas fees, which is not cheap on Ethereum) to avoid it.

## Code Snippet

- https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/point-tokenization-vault/contracts/PointTokenVault.sol#L164-L167

## Tool used

Manual Review

## Recommendation

Only allow user or trusted addresses to call `claimPTokens()` to claim their pTokens.