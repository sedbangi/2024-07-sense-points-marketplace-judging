Original Beige Leopard

Medium

# Fee-on-transfer token is not supported for reward tokens during `convertRewardsToPTokens`


## Summary

Fee-on-transfer token is not supported for reward tokens during `convertRewardsToPTokens`. Users would receive more pTokens than expected.

## Vulnerability Detail

First, we should understand that the PointTokenVault supports fee-on-transfer tokens. Quoting from the contest readme:

> Q: If you are integrating tokens, are you allowing only whitelisted tokens to work with the codebase or any complying with the standard? Are they assumed to have certain properties, e.g. be non-reentrant? Are there any types of weird tokens you want to integrate?

> They're assumed to not be reentrant, but fee-on-transfer, pausable, and blocklist tokens are OK.

The code comments claim that fee-on-transfer tokens must be wrapped before depositing. However, this is only the case for the staking tokens, and not for reward tokens. Since the protocol has a whitelisting feature (setting the `cap[]` for a token), the protocol can control which staking token to be added.

```solidity
>   // Rebasing and fee-on-transfer tokens must be wrapped before depositing.
    function deposit(ERC20 _token, uint256 _amount, address _receiver) public {
        uint256 cap = caps[address(_token)];

        if (cap != type(uint256).max) {
            if (_amount + _token.balanceOf(address(this)) > cap) {
                revert DepositExceedsCap();
            }
        }

        _token.safeTransferFrom(msg.sender, address(this), _amount);

        balances[_receiver][_token] += _amount;

        emit Deposit(msg.sender, _receiver, address(_token), _amount);
    }
```

Actually, the PointTokenVault contract itself has no control over whether a *reward token* is fee-on-transfer or not. Thus the protocol must be expected to handle such cases.

The issue lies in the `convertRewardsToPTokens` function, where users can convert their reward tokens back to pTokens. In this function, the user specify the amount of `_amountToConvert` reward tokens to convert, however, this is the amount BEFORE transfer fees are taken. Example:

1. User converts 100 reward tokens back to pToken.
2. 5% of transfer fee is taken. User only deposits 95 pToken.
3. However, the amount of pToken user receives is based on the pre-fee amount 100 reward token.

In the end, users will receive more pTokens than they should. The correct implementation should be calculating the pTokens based on reward token balance diff.

```solidity
    function convertRewardsToPTokens(address _receiver, bytes32 _pointsId, uint256 _amountToConvert) public {
        RedemptionParams memory params = redemptions[_pointsId];
        (ERC20 rewardToken, uint256 rewardsPerPToken, bool isMerkleBased) =
            (params.rewardToken, params.rewardsPerPToken, params.isMerkleBased);

        if (address(rewardToken) == address(0)) {
            revert RewardsNotReleased();
        }

        if (isMerkleBased) {
            revert CantConvertMerkleRedemption();
        }

        rewardToken.safeTransferFrom(msg.sender, address(this), _amountToConvert);

        // ISSUE: Should use balance difference instead.
>       uint256 pTokensToMint = FixedPointMathLib.divWadDown(_amountToConvert, rewardsPerPToken); // Round down for mint.

        // Dust guard.
        if (pTokensToMint == 0) {
            revert AmountTooSmall();
        }

        pTokens[_pointsId].mint(_receiver, pTokensToMint);

        emit RewardsConverted(msg.sender, _receiver, _pointsId, _amountToConvert);
    }
```

## Impact

Users will receive more pTokens than they should when the call `convertRewardsToPTokens`.

## Code Snippet

- https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/point-tokenization-vault/contracts/PointTokenVault.sol#L229

## Tool used

Manual Review

## Recommendation

Calculate the rewardToken balance diff before and after token transfer. Use this number to calculate the pToken amount instead.