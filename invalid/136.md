Alert Sage Tiger

High

# Users can steal funds using tokens with max transfer logic

### Summary

Some tokens such as `cUSDCv3` contain a special case for `amount == type(uint256).max` in their transfer functions that results in only the user's balance being transferred. This can be used to steal other depositors funds when the user calls `deposit(...)` with `type(uint256).max`.

### Root Cause

A user who has 10 `cUSDCv3` can call [`deposit(...)`](https://github.com/sherlock-audit/2024-07-sense-points-marketplace/blob/main/point-tokenization-vault/contracts/PointTokenVault.sol#L114-L128) with `_amount = type(uint256).max` when the `cap = type(uint256).max`. The `cUSDCv3.transferFrom(...)` is called which safely handles the deposit of 10 cUSDCv3 but the `PointTokenVault` contract uses the user entered amount for its accounting and as such, `balances[][]` will be `type(uint256).max`.
The user then calls `withdraw(...)` with an arbitrary amount of their choice to steal funds from the `PointTokenVault`

The  `cUSDCv3` `transferFrom` function is as shown below

```solidity
    function transferFrom(address src, address dst, uint amount) override external returns (bool) {
        transferInternal(msg.sender, src, dst, baseToken, amount);
        return true;
    }

    function transferInternal(address operator, address src, address dst, address asset, uint amount) internal {
        if (isTransferPaused()) revert Paused();
        if (!hasPermission(src, operator)) revert Unauthorized();
        if (src == dst) revert NoSelfTransfer();

        if (asset == baseToken) {
   @>       if (amount == type(uint256).max) {
                amount = balanceOf(src);
            }
            return transferBase(src, dst, amount);
        } else {
            return transferCollateral(src, dst, asset, safe128(amount));
        }
    }

```

### Internal pre-conditions

1. Per the audit README the `cap` can be set to any value,

> Are there any limitations on values set by admins (or other roles) in the codebase, including restrictions on array lengths?

> No

2. Also the protocol mentions that they would use whitelisted tokens but did not give a list of such tokens, hence if `cUSDCv3` is whitelisted for use in the protocol, then the vulnerability described in this report is of a high likelihood and impact.

### External pre-conditions

_No response_

### Attack Path

1. Alice deposit her 10,000 cUSDCv3
2. Bob deposits his 5, 000 cUSDCv3
3. Carol has 10 cUSDCv3 and deposits as well with `_amount` = `type(uint256).max`
4. Carol immediately call `withdraw(...)` with `_amount` = 15,010 cUSDCv3
5. Carol gains 15,010 cUSDCv3

### Impact

The attacker can drain the vault and steal all the deposited funds.

### PoC

_No response_

### Mitigation

Modify the `PointTokenVault::deposit(..)` function as shown below

```diff
    function deposit(ERC20 _token, uint256 _amount, address _receiver) public {
        uint256 cap = caps[address(_token)];

        if (cap != type(uint256).max) {
            if (_amount + _token.balanceOf(address(this)) > cap) {
                revert DepositExceedsCap();
            }
        }
+      uint balanceBefore = _token.balanceOf(address(this));
        _token.safeTransferFrom(msg.sender, address(this), _amount); 
+      _amount = _token.balanceOf(address(this)) - balanceBefore;
        balances[_receiver][_token] += _amount; 

        emit Deposit(msg.sender, _receiver, address(_token), _amount);
    }

```